apply plugin: 'com.android.application'
apply from: '../findbugs.gradle'
apply from: '../pmd.gradle'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled false
        }
    }
    defaultConfig {
        applicationId "de.tum.in.tumcampus"
        minSdkVersion 10
        targetSdkVersion 24
        multiDexEnabled true
        versionCode 67
        versionName "1.4.5-dev"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        checkAllWarnings true
        htmlReport false
        warningsAsErrors true
        abortOnError true
        lintConfig new File("lint.xml")
        disable 'GoogleAppIndexingWarning',
                'TrulyRandom',
                'ContentDescription',
                'UnusedResources',    // unused resources get purged by shrinkResources
                'SelectableText'      // disabled by default
    }
    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
    productFlavors {
        v10 {
            minSdkVersion 10
        }
        v14 {
            minSdkVersion 14
            lintOptions.abortOnError false
        }
    }

    publishNonDefault true
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:appcompat-v7:24.2.1'
        force 'com.android.support:design:24.2.1'
        force 'com.android.support:recyclerview-v7:24.2.1'
        force 'com.google.code.findbugs:jsr305:3.0.1'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:preference-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.google.android.gms:play-services-gcm:9.4.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.google.guava:guava:19.0'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'se.emilsjolander:stickylistheaders:2.7.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.commit451:PhotoView:1.2.4'
    compile 'me.dm7.barcodescanner:zxing:1.9'
    compile('org.simpleframework:simple-xml:2.7.1') {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'xpp3', module: 'xpp3'
    }
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.1'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.7.5'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.github.alamkanak:android-week-view:1.2.6'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'net.danlew:android.joda:2.9.4'

    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
}
